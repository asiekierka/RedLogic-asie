buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
		maven {
			url = "https://jitpack.io"
		}
	}
	dependencies {
		classpath 'com.github.CDAGaming:ForgeGradle:1c670759c5'
	}
}

repositories {
	maven {
		name = "chickenbones"
		url = "http://chickenbones.net/maven/"
	}
}

apply plugin: 'forge'

file "build.properties" withReader {
	def prop = new Properties()
	prop.load(it)
	ext.config = new ConfigSlurper().parse prop
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

version = "${config.minecraft.version}-${config.redlogic.version}"
group = "mods.immibis.redlogic" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "RedLogic"

dependencies {
    compile "codechicken:CodeChickenLib:1.7.10-1.1.3.127:dev"
    compile "codechicken:CodeChickenCore:1.7.10-1.0.3.26:dev"
    compile "codechicken:NotEnoughItems:1.7.10-1.0.3.67:dev"
    compile "junit:junit:4.13"
}

if (JavaVersion.current().isJava8Compatible()) {
	allprojects {
		tasks.withType(Javadoc) {
			options.addStringOption('Xdoclint:none', '-quiet')
		}
	}
}

configurations {
	provided
	embedded
	compile.extendsFrom provided, embedded
}

minecraft {
	version = "${config.minecraft.version}-${config.forge.version}"
	runDir = "eclipse/assets"
}

sourceSets {
	main {
		output.resourcesDir = 'build/classes/main'
	}
}
apply plugin: 'idea'
idea {
	module {
		scopes.PROVIDED.plus += [configurations.provided]
		outputDir = file('build/classes/main')
	}
}

processResources
	{
		// replace stuff in mcmod.info, nothing else
		from(sourceSets.main.resources.srcDirs) {
			include 'mcmod.info'

			// replace version and mcversion
			expand 'version': project.version, 'mcversion': project.minecraft.version
		}

		// copy everything else, thats not the mcmod.info
		from(sourceSets.main.resources.srcDirs) {
			exclude 'mcmod.info'
		}
	}
